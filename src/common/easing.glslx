#include "./constants.glslx"

/* Easing Back In equation */
/* Adapted from Robert Penner easing equations */
float easeBackIn(float t) {
    float s = 1.70158;
    return t * t * ((s + 1.0) * t - s);
}

/* Easing Back InOut equation */
/* Adapted from Robert Penner easing equations */
float easeBackInOut(float t) {
    t *= 2.0; float s = 1.70158;
    if (t < 1.0) return 0.5 * (t * t * (((s *= (1.525)) + 1.0) * t - s));
    return 0.5 * ((t -= 2.0) * t * (((s *= (1.525)) + 1.0) * t + s) + 2.0);
}

/* Easing Back Out equation */
/* Adapted from Robert Penner easing equations */
float easeBackOut(float t) {
    float s = 1.70158;
    return ((t = t - 1.0) * t * ((s + 1.0) * t + s) + 1.0);
}

/* Easing Bounce In equation */
/* Adapted from Robert Penner easing equations */
float easeBounceOut(float t) {
    if (t < (1.0 / 2.75)) {
        return (7.5625 * t * t);
    } else if (t < (2.0 / 2.75)) {
        return (7.5625 * (t -= (1.5 / 2.75)) * t + 0.75);
    } else if (t < (2.5 / 2.75)) {
        return (7.5625 * (t -= (2.25 / 2.75)) * t + 0.9375);
    } else {
        return (7.5625 * (t -= (2.625 / 2.75)) * t + 0.984375);
    }
}

float easeBounceIn(float t) {
    return 1.0 - easeBounceOut(1.0 - t);
}

float easeBounceInOut(float t) {
    if (t < 0.5) return easeBounceIn(t * 2.0) * 0.5;
    else return easeBounceOut(t * 2.0 - 1.0) * 0.5 + 0.5;
}

/* Easing Circular In equation */
/* Adapted from Robert Penner easing equations */
float easeCircularIn(float t) {
    return -1.0 * (sqrt(1.0 - t * t) - 1.0);
}

/* Easing Circular InOut equation */
/* Adapted from Robert Penner easing equations */
float easeCircularInOut(float t) {
    t = t * 2.0; if ((t) < 1.0) return -0.5 * (sqrt(1.0 - t * t) - 1.0);
    return 0.5 * (sqrt(1.0 - (t -= 2.0) * t) + 1.0);
}

/* Easing Circular Out equation */
/* Adapted from Robert Penner easing equations */
float easeCircularOut(float t) {
    return sqrt(1.0 - (t = t - 1.0) * t);
}

/* Easing Cubic In equation */
/* Adapted from Robert Penner easing equations */
float easeCubicIn(float t) {
    return t * t * t;
}

/* Easing Cubic InOut equation */
/* Adapted from Robert Penner easing equations */
float easeCubicInOut(float t) {
    t = t * 2.0; if (t < 1.0) return 0.5 * t * t * t;
    return 0.5 * ((t -= 2.0) * t * t + 2.0);
}

/* Easing Cubic Out equation */
/* Adapted from Robert Penner easing equations */
float easeCubicOut(float t) {
    return ((t = t - 1.0) * t * t + 1.0);
}

/* Easing Elastic In equation */
/* Adapted from Robert Penner easing equations */
float easeElasticIn(float t) {
    if (t == 0.0) { return 0.0; }
    if (t == 1.0) { return 1.0; }
    float p = 0.3;
    float a = 1.0;
    float s = p / 4.0;
    return -(a * pow(2.0, 10.0 * (t -= 1.0)) * sin((t - s) * TWO_PI / p));
}

/* Easing Elastic InOut equation */
/* Adapted from Robert Penner easing equations */
float easeElasticInOut(float t) {
    t = t * 2.0;
    if (t == 0.0) { return 0.0; }
    if ((t / 2.0) == 2.0) { return 1.0; }
    float p = (0.3 * 1.5);
    float a = 1.0;
    float s = p / 4.0;
    if (t < 1.0) {
        return -0.5 * (a * pow(2.0, 10.0 * (t -= 1.0)) * sin((t - s) * TWO_PI / p));
    }
    return a * pow(2.0, -10.0 * (t -= 1.0)) * sin((t - s) * TWO_PI / p) * 0.5 + 1.0;
}

/* Easing Elastic Out equation */
/* Adapted from Robert Penner easing equations */
float easeElasticOut(float t) {
    if (t == 0.0) { return 0.0; }
    if (t == 1.0) { return 1.0; }
    float p = 0.3;
    float a = 1.0;
    float s = p / 4.0;
    return (a * pow(2.0, -10.0 * t) * sin((t - s) * TWO_PI / p) + 1.0);
}

/* Easing Expo In equation */
/* Adapted from Robert Penner easing equations */
float easeExpoIn(float t) {
    return (t == 0.0) ? 0.0 : pow(2.0, 10.0 * (t - 1.0));
}

/* Easing Expo InOut equation */
/* Adapted from Robert Penner easing equations */
float easeExpoInOut(float t) {
    t = t * 2.0;
    if (t == 0.0) return 0.0;
    if (t == 1.0) return 1.0;
    if (t < 1.0) return 0.5 * pow(2.0, 10.0 * (t - 1.0));
    return 0.5 * (-pow(2.0, -10.0 * --t) + 2.0);
}

/* Easing Expo Out equation */
/* Adapted from Robert Penner easing equations */
float easeExpoOut(float t) {
    return (t == 1.0) ? 1.0 : (-pow(2.0, -10.0 * t) + 1.0);
}

/* Easing Quad In equation */
/* Adapted from Robert Penner easing equations */
float easeQuadIn(float t) {
    return t * t;
}

/* Easing Quad InOut equation */
/* Adapted from Robert Penner easing equations */
float easeQuadInOut(float t) {
    t = t * 2.0; if (t < 1.0) return 0.5 * t * t;
    return -0.5 * ((--t) * (t - 2.0) - 1.0);
}

/* Easing Quad Out equation */
/* Adapted from Robert Penner easing equations */
float easeQuadOut(float t) {
    return -1.0 * t * (t - 2.0);
}

/* Easing Quart In equation */
/* Adapted from Robert Penner easing equations */
float easeQuartIn(float t) {
    return t * t * t * t;
}

/* Easing Quart InOut equation */
/* Adapted from Robert Penner easing equations */
float easeQuartInOut(float t) {
    t = t * 2.0; if (t < 1.0) return 0.5 * t * t * t * t;
    return -0.5 * ((t -= 2.0) * t * t * t - 2.0);
}

/* Easing Quart Out equation */
/* Adapted from Robert Penner easing equations */
float easeQuartOut(float t) {
    return -1.0 * ((t = t - 1.0) * t * t * t - 1.0);
}

/* Easing Quint In equation */
/* Adapted from Robert Penner easing equations */
float easeQuintIn(float t) {
    return t * t * t * t * t;
}

/* Easing Quint InOut equation */
/* Adapted from Robert Penner easing equations */
float easeQuintInOut(float t) {
    t = t * 2.0; if (t < 1.0) return 0.5 * t * t * t * t * t;
    return 0.5 * ((t -= 2.0) * t * t * t * t + 2.0);
}

/* Easing Quint Out equation */
/* Adapted from Robert Penner easing equations */
float easeQuintOut(float t) {
    return ((t = t - 1.0) * t * t * t * t + 1.0);
}

/* Easing Sine In equation */
/* Adapted from Robert Penner easing equations */
float easeSineIn(float t) {
    return -1.0 * cos(t * PI_TWO) + 1.0;
}

/* Easing Sine InOut equation */
/* Adapted from Robert Penner easing equations */
float easeSineInOut(float t) {
    return -0.5 * (cos(PI * t * 2.0) - 1.0);
}

/* Easing Sine Out equation */
/* Adapted from Robert Penner easing equations */
float easeSineOut(float t) {
    return sin(t * PI_TWO);
}
